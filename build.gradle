plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.24'
    id 'org.springframework.boot' version '3.3.1'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.24'
    id 'jacoco'
}

repositories {
    mavenLocal()
    maven {
        url uri('lib')
    }
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
    }
}

def springBootVersion = '3.3.1'

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-graphql:${springBootVersion}"
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'javax.validation:validation-api:2.0.1.Final'

    testImplementation 'io.specmatic:junit5-support:2.0.0'
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation "io.specmatic:specmatic-graphql:1.0.0-TRIAL"
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/kotlin')
            exclude '**/*.java'
            exclude '**/*.kt}'
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            def resultLine = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def lineLength = resultLine.length()
            def separator = "-" * lineLength

            println separator
            println resultLine
            println separator        }
    }
}

group = 'in.specmatic.sample'
version = '1.0'

configurations {
    configureEach {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}
